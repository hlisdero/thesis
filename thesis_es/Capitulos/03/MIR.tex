\section{Mid-level Intermediate Representation (MIR)}

En esta sección se ofrece una visión general de la \acrfull{MIR}.
La \acrshort{MIR} se introdujo
en la RFC 1211\footnote{\url{https://rust-lang.github.io/rfcs/1211-mir.html}}
en agosto de 2015.
Exploraremos sus diferentes partes,
cómo se mapean en ellas diferentes fragmentos de código
y la estructura de grafos subyacente.

\begin{listing}[!htb]
    \begin{minted}{Rust}
        fn main() {
            match std::env::args().len() {
                1 => 2,
                3 => 6,
                _ => 0,
            };
        }
    \end{minted}
    \caption{Programa Rust sencillo para explicar los componentes de la MIR.}
    \label{lst:rust-code-example}
\end{listing}

\begin{longlisting}
    \begin{minted}{Rust}
        // WARNING: This output format is intended for human consumers only
        // and is subject to change without notice. Knock yourself out.
        fn main() -> () {
            let mut _0: ();               // return place in scope 0 at src/main.rs:1:11: 1:11
            let mut _1: usize;            // in scope 0 at src/main.rs:2:11: 2:33
            let mut _2: &std::env::Args;  // in scope 0 at src/main.rs:2:11: 2:33
            let _3: std::env::Args;       // in scope 0 at src/main.rs:2:11: 2:27

            bb0: {
                _3 = args() -> bb1;       // scope 0 at src/main.rs:2:11: 2:27
                                          // mir::Constant
                                          // + span: src/main.rs:2:11: 2:25
                                          // + literal: Const { ty: fn() ->
                                          //   Args {args}, val: Value(<ZST>) }
            }

            bb1: {
                _2 = &_3;                 // scope 0 at src/main.rs:2:11: 2:33
                _1 = <Args as ExactSizeIterator>::len(move _2) -> [return: bb2, unwind: bb4];
                                          // scope 0 at src/main.rs:2:11: 2:33
                                          // mir::Constant
                                          // + span: src/main.rs:2:28: 2:31
                                          // + literal: Const { ty: for<'a> fn(&'a Args) ->
                                          //   usize {<Args as ExactSizeIterator>::len},
                                          //   val: Value(<ZST>) }
            }

            bb2: {
                drop(_3) -> bb3;          // scope 0 at src/main.rs:6:6: 6:7
            }

            bb3: {
                return;                   // scope 0 at src/main.rs:7:2: 7:2
            }

            bb4 (cleanup): {
                drop(_3) -> [return: bb5, unwind terminate]; // scope 0 at src/main.rs:6:6: 6:7
            }

            bb5 (cleanup): {
                resume;                   // scope 0 at src/main.rs:1:1: 7:2
            }
        }
    \end{minted}
    \caption{MIR del Listado \ref{lst:rust-code-example}
        compilado utilizando rustc 1.71.0-nightly en modo debug.}
    \label{lst:mir-output-debug-example}
\end{longlisting}

Considere el código de ejemplo que aparece en el Listado \ref{lst:rust-code-example},
el \acrshort{MIR}\footnote{Los comentarios de la MIR se han modificado ligeramente para mejorar el resultado}
correspondiente se muestra en el Listado \ref{lst:mir-output-debug-example}.
Observe la advertencia explícita en la parte superior de la salida generada. Se omitirá en los
listados posteriores por simplicidad.
Además, la salida depende de los siguientes factores:

\begin{itemize}
    \item La versión de \emph{rustc} en uso, alternativamente el canal de lanzamiento (estable, beta o nightly).
    \item El tipo de compilación: \emph{debug} o \emph{release}. Por defecto, el comando cargo build genera un
          \emph{debug build}, mientras que \texttt{cargo build --release} produce un \emph{release build}.
\end{itemize}

Para ilustrar esta variabilidad, el Listado \ref{lst:mir-output-release-example}
muestra la salida al compilar el mismo programa en modo \emph{release}.
La característica distintiva que se encuentra en las compilaciones \emph{release} es la
presencia de las sentencias \Rustinline{StorageLive} y \Rustinline{StorageDead}.
Por otro lado, las compilaciones de depuración (\emph{debug}) generan \acrshort{MIR} más cortos y claros
que se acercan más a lo que escribió el usuario.
Por esta razón, a menos que se indique lo contrario, los Listados de este trabajo contienen \acrshort{MIR}
generados en \emph{debug builds}.

\begin{longlisting}
    \begin{minted}{Rust}
        // WARNING: This output format is intended for human consumers only
        // and is subject to change without notice. Knock yourself out.
        fn main() -> () {
            let mut _0: ();               // return place in scope 0 at src/main.rs:1:11: 1:11
            let mut _1: usize;            // in scope 0 at src/main.rs:2:11: 2:33
            let mut _2: &std::env::Args;  // in scope 0 at src/main.rs:2:11: 2:33
            let _3: std::env::Args;       // in scope 0 at src/main.rs:2:11: 2:27

            bb0: {
                StorageLive(_1);          // scope 0 at src/main.rs:2:11: 2:33
                StorageLive(_2);          // scope 0 at src/main.rs:2:11: 2:33
                StorageLive(_3);          // scope 0 at src/main.rs:2:11: 2:27
                _3 = args() -> bb1;       // scope 0 at src/main.rs:2:11: 2:27
                                          // mir::Constant
                                          // + span: src/main.rs:2:11: 2:25
                                          // + literal: Const { ty: fn() ->
                                          //   Args {args},
                                          //   val: Value(<ZST>) }
            }

            bb1: {
                _2 = &_3;                 // scope 0 at src/main.rs:2:11: 2:33
                _1 = <Args as ExactSizeIterator>::len(move _2) -> [return: bb2, unwind: bb4];
                                          // scope 0 at src/main.rs:2:11: 2:33
                                          // mir::Constant
                                          // + span: src/main.rs:2:28: 2:31
                                          // + literal: Const { ty: for<'a> fn(&'a Args) ->
                                          //   usize {<Args as ExactSizeIterator>::len},
                                          //   val: Value(<ZST>) }
            }

            bb2: {
                StorageDead(_2);          // scope 0 at src/main.rs:2:32: 2:33
                drop(_3) -> bb3;          // scope 0 at src/main.rs:6:6: 6:7
            }

            bb3: {
                StorageDead(_3);          // scope 0 at src/main.rs:6:6: 6:7
                StorageDead(_1);          // scope 0 at src/main.rs:6:6: 6:7
                return;                   // scope 0 at src/main.rs:7:2: 7:2
            }

            bb4 (cleanup): {
                drop(_3) -> [return: bb5, unwind terminate]; // scope 0 at src/main.rs:6:6: 6:7
            }

            bb5 (cleanup): {
                resume;                   // scope 0 at src/main.rs:1:1: 7:2
            }
        }
    \end{minted}
    \caption{MIR del Listado \ref{lst:rust-code-example}
        compilado usando rustc 1.71.0-nightly en modo release.}
    \label{lst:mir-output-release-example}
\end{longlisting}

El formato específico al convertir \acrshort{MIR} a una cadena de texto sólo ha cambiado ligeramente con el
tiempo. Consulte \cite[Section 3.3]{meyer2020} para ver un ejemplo de salida más antiguo de
mediados de 2019.

Como se indica en la Sec. \ref{sec:interception-strategy}, la \acrshort{MIR} se deriva
de un grafo de flujo de control (\acrfull{CFG}) previamente existente en el compilador Rust.
Fundamentalmente, un \acrshort{CFG} es una
representación gráfica de un programa que expone el flujo de control subyacente.

\subsection{Componentes de la MIR}
\label{sec:mir-components}

La \acrshort{MIR} está formada por funciones.
Cada función se representa como una serie de bloques
básicos (\acrfull{BB}) conectados por aristas dirigidas.
Cada \acrshort{BB} contiene cero o más sentencias o \emph{statements}
(normalmente abreviadas como ``STMT'') y por último una sentencia terminadora (\emph{terminator statement}), para
abreviar \emph{terminator}.
El terminator es la única sentencia en la que el programa puede emitir una
instrucción que dirija el flujo de control a otro bloque básico dentro de la misma función o para
llamar a otra función.
Las bifurcaciones como en las sentencias \Rustinline{match} o \Rustinline{if} de Rust sólo pueden
producirse en los terminators. Los terminators desempeñan el papel de mapear las
construcciones de alto nivel para la ejecución condicional y los bucles a la representación de
bajo nivel en código máquina como simples instrucciones \Rustinline{branch} de bifurcación condicional o
incondicional.

En la Fig. \ref{fig:mir-cfg-example} se presenta
la representación gráfica de la \acrshort{MIR} que aparece en el
Listado \ref{lst:mir-output-debug-example}.
Los statements están coloreados en azul claro y los terminators en rojo claro.
Para que el tipo de declaración terminadora sea más claro,
se han añadido anotaciones adicionales como en \texttt{CALL:} o \texttt{DROP:}.

\begin{figure}[!htb]
    \centering
    \includesvg[width=0.55\linewidth]{mir-cfg-example.svg}
    \caption{Representación gráfica del flujo de control de la MIR mostrada en el Listado \ref{lst:mir-output-debug-example}.}
    \label{fig:mir-cfg-example}
\end{figure}

Debe tenerse en cuenta que la llamada a la función \Rustinline{std::env::args().len()}
en la línea 2 del Listado \ref{lst:rust-code-example} puede retornar con éxito o fallar.
Un fallo desencadena un desenrollado de la pila (\emph{stack unwinding}),
finalizando el programa e informando de un error.
Esto está representado por la bifurcación al final de BB1, donde la
ejecución del código puede tomar el camino de la izquierda o el de la derecha en el gráfico. La
bifurcación izquierda (BB4 y BB5) corresponde a la ejecución correcta del programa, mientras
que la bifurcación derecha se refiere a la terminación anormal del programa.

Existen diferentes tipos de terminadores y éstos son específicos de la semántica de Rust.
Presentaremos algunos de ellos para aclarar el significado del ejemplo presentado.

\begin{itemize}
    \item Como era de esperar, un terminador de tipo \texttt{CALL:} llama a una función, que devuelve un
          valor, y continúa la ejecución hasta el siguiente \acrshort{BB}.
    \item Un terminador de tipo \texttt{DROP:} libera la memoria de la variable pasada. Ejecuta los
          destructores\footnote{\url{https://doc.rust-lang.org/stable/reference/destructors.html}}
          y realiza todas las tareas de limpieza necesarias. A partir de ese momento, la
          variable ya no puede utilizarse en el programa.
    \item \texttt{RETURN:} retorna de la función. El valor de retorno se almacena siempre en la variable local
          \Rustinline{_0}, como veremos en breve.
    \item \texttt{RESUME:} indica que el proceso debe continuar desenrollándose (\textit{unwinding}).
          De forma análoga a un retorno, esto marca el final de esta invocación de la función. Sólo se permite en bloques de
          limpieza.
\end{itemize}

La lista completa de tipos de terminadores puede consultarse en la documentación
nightly\footnote{\url{https://doc.rust-lang.org/stable/nightly-rustc/rustc_middle/mir/enum.TerminatorKind.html}}.
Otros tipos de terminadores se tratarán en detalle en la Sec. \ref{sec:terminators}.

En cuanto a las variables, los datos en \acrshort{MIR} pueden dividirse en dos categorías: \emph{locals} y
\emph{places}. Es sumamente importante observar que estos ``places'' no están relacionados con los lugares o \emph{places} de
las redes de Petri.
Los places se utilizan para representar todo tipo de ubicaciones de memoria
(incluidos los alias), mientras que las locals se limitan a las ubicaciones de memoria basadas
en la pila (\emph{stack}), es decir, las variables locales de una función.
En otras palabras, los \emph{places} son más
generales y las \emph{locals} son un caso especial de un lugar,
por lo que los places no siempre son equivalentes a las locals.
Convenientemente, todos los lugares son también locales en la Fig. \ref{fig:mir-cfg-example}.

Los locales se identifican mediante un índice no negativo creciente y son emitidos por el
compilador como una cadena de la forma ``\Rustinline{_<index>}''.
En particular, el valor de retorno de la función siempre se almacena en el primer local \Rustinline{_0}.
Esto coincide estrechamente con la representación de bajo nivel en la pila.

\subsection{Ejemplo paso a paso}

En esta subsección, daremos una breve explicación de lo que ocurre en cada bloque básico de
la Fig. \ref{fig:mir-cfg-example} para cubrir toda la información necesaria
para las siguientes secciones.
Por otra parte, esto ilustra cómo la salida de la \acrshort{MIR} representa
construcciones de nivel superior que se encuentran a menudo al programar en Rust.

\subsubsection{BB0}

\begin{itemize}
    \item La función \Rustinline{main()} comienza en BB0.
    \item Se llama a una función (\Rustinline{std::env::args()}) para obtener
          un iterador sobre los argumentos pasados al programa.
    \item El valor de retorno de la función, el iterador, se asigna a la local \Rustinline{_3}.
    \item La ejecución continúa en BB1.
\end{itemize}

\subsubsection{BB1}

\begin{itemize}
    \item Se genera una referencia al iterador almacenado en \Rustinline{_3}
          y se almacena en el local \Rustinline{_2} (similar al operador ``\&'' en C).
          Esto es necesario para llamar a métodos porque los
          métodos reciben una referencia a un struct del mismo tipo (\Rustinline{&self}) como primer
          argumento.
    \item La referencia almacenada en \Rustinline{_2} se pasa por movimiento
          al método \Rustinline{std::env::Args::len()}
          y se llama a la función.
    \item El valor de retorno de la función, el número de argumentos pasados a la función,
          se asigna al local \Rustinline{_1}.
    \item La ejecución continúa en BB2 si tiene éxito, en BB4 en caso de \textit{panic}.
\end{itemize}

\subsubsection{BB2}

\begin{itemize}
    \item La variable \Rustinline{_3}, cuyo valor es el iterador sobre los argumentos,
          se elimina (\emph{dropped}) puesto que ya no es necesaria.
    \item La ejecución continúa en BB3.
\end{itemize}

\subsubsection{BB3}

\begin{itemize}
    \item La función retorna.
          El valor de retorno (local \Rustinline{_0}) es de tipo ``unit''\footnote{\url{https://doc.rust-lang.org/std/primitive.unit.html}}
          que es similar a una función \texttt{void} en C, es decir, no devuelve nada.
          Así es como se definió \Rustinline{main()} en el Listado \ref{lst:rust-code-example}.
\end{itemize}

\subsubsection{BB4}

\begin{itemize}
    \item La variable \Rustinline{_3}, cuyo valor es el iterador sobre los argumentos,
          se elimina (\emph{drop}) puesto que ya no es necesaria.
    \item Si el \textit{drop} tiene éxito, la ejecución continúa en BB5,
          de lo contrario, finaliza el programa inmediatamente.
\end{itemize}

\subsubsection{BB5}

\begin{itemize}
    \item Continúe desenrollando la pila. Este es el protocolo estándar definido para manejar los
          casos de error catastrófico que no pueden ser manejados por el programa. Los detalles
          de implementación se pueden encontrar en la documentación\footnote{\url{https://rustc-dev-guide.rust-lang.org/panic-implementation.html}}
\end{itemize}